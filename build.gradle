plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.10.0'
}

allure {
    report {
        version.set("2.19.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.19.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation (
            'org.junit.jupiter:junit-jupiter:5.8.2',
            'com.codeborne:selenide:6.8.1',
            'org.assertj:assertj-core:3.23.1',
            'com.codeborne:pdf-test:1.7.0',
            'com.codeborne:xls-test:1.4.3',
            'com.opencsv:opencsv:5.7.0',
            'com.google.code.gson:gson:2.9.1',
            'com.github.javafaker:javafaker:1.0.2',
            'com.fasterxml.jackson.core:jackson-databind:2.13.4',
            'io.qameta.allure:allure-selenide:2.18.1',
            "org.aspectj:aspectjweaver:1.9.6",
            "org.aeonbits.owner:owner:1.0.12",
            "org.slf4j:slf4j-simple:1.7.29"
    )

    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.9.0')

}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
//            events "started", "failed"
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}
